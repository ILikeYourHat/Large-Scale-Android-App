apply plugin: "kotlin-android"
apply from: "$rootDir/gradle/libs/lint_android.gradle"
apply from: "$rootDir/gradle/libs/detekt.gradle"

android {
    compileSdk libs.versions.compileSdk.get() as int
    defaultConfig {
        minSdk libs.versions.minSdk.get() as int
        targetSdk libs.versions.targetSdk.get() as int
    }

    namespace "com.github.ilikeyourhat.lsaa" + project.path.replace(":", ".")

    kotlinOptions {
        kotlinOptions.allWarningsAsErrors = true
        freeCompilerArgs += [
                "-Xexplicit-api=strict"
        ]
        jvmTarget = libs.versions.java.get()
    }
    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility = libs.versions.java.get()
        targetCompatibility = libs.versions.java.get()
    }
}

dependencies {
    coreLibraryDesugaring libs.desugaring
}